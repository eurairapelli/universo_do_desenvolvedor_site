#INCLUDE "TOTVS.CH"

/*/{Protheus.doc} DDDBRASILAPI
Consulta de DDD via BRASIL API
https://brasilapi.com.br/docs#tag/Brasil-API

@type		function
@author		Eurai Rapelli
@since 		2021.08.28
/*/
User Function DDDBRASILAPI()
	Local oDlg		   	 := Nil AS Object
	Local oSayXX		  := Nil AS Object

	Private oGet01		 := Nil AS Object
	Private cGet01		 := Space(02) AS character

	Private oMemo11		 := Nil AS Object
	Private cMemo01		 := '' AS character

	Private cCRLF 		:= CRLF as character



	oDlg		        := MSDialog():New( 000,000,550,430,'DDD - BRASILAPI',,,.F.,,,,,,.T.,,,.T. )
	oDlg:lMaximized     := .T.


	oSayXX 		:= TSay():New( 010,010,{|| 'DDD:' },oDlg,,,.F.,.F.,.F.,.T.,,,100,008)
	oGet01 		:= TGet():New( 010,060,bSETGET(cGet01),oDlg,120,010,,,,/*10*/,,,,.T.,/*15*/,,,,,/*20*/,,)
	oGet01:bValid := {|UniversoDesenvolvedor| bValid() }

	oSayXX 		:= TSay():New( 030,010,{|| 'Cidades:' },oDlg,,,.F.,.F.,.F.,.T.,,,100,008)
	oMemo11 	:= TMultiget():New(040,010,{|u|if(Pcount()>0,cMemo01:=UPPER(u),cMemo01)},oDlg,200,200,,.T.,,,,.T.,,,,,,,,,,,.T.)
	oMemo11:lReadOnly	:= .T.




	oDlg:Activate(,,,.T.)

Return( Nil )


/*/{Protheus.doc} bValid
Validação

@type		function
@author		Eurai Rapelli
@since 		2021.08.28
/*/
Static Function bValid()
	Local lRet          := .T. as logical
	Local aHeader       := {} as array
	Local cHeaderRet    := '' as character
	Local cResult       := '' as character
	Local oResult       := {} as object


	Local nI 			:= 00 as numeric

	Begin Sequence

		If Empty(cGet01)
			MsgInfo('Informe o DDD','Universo do Desenvolvedor')
			Break
		Endif



		cResult := HTTPQuote('https://brasilapi.com.br/api/ddd/v1/'+AllTrim(cGet01), "GET", "", , , aHeader, @cHeaderRet)
		If !("200 OK" $ cHeaderRet )
			MsgInfo('Erro na Consulta: ' + cResult,'Universo do Desenvolvedor')
			Break
		Endif

		If !FWJsonDeserialize( cResult, @oResult )
			MsgInfo('Erro no jSon: ' + cResult,'Universo do Desenvolvedor')
			Break
		Endif

		cMemo01  := ''
		For nI := 01 to Len(oResult:cities)
			cMemo01  += DecodeUTF8(oResult:cities[nI]) + cCRLF
		Next nI

		RECOVER


	End Sequence

Return( lRet )



